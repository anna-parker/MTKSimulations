configfile: "../config.json"
REC = config.get("REC")
SIMTYPE = config.get("SIMTYPE")
RES = config.get("RES")
STRICT = config.get("STRICT")

NAMES = ["tp", "fp", "tn", "fn"]

rule all:
    input:
        "results.zip"

rule precompile_script:
    message:
        "precompile julia script"
    output:
        "accuracy_shared_branches.so",
    shell:
        """
        julia --project=../. build_sys_image.jl
        rm -r results
        """

rule run_simulations:
    message:
        "start simulations in julia"
    input:
        "accuracy_shared_branches.so"
    output:
        expand(["results/results_{{simtype}}_{{res}}_{{strict}}/results_{n}_{{rec}}.txt"], n=NAMES)
    params: 
        n_leaves = config.get("n"),
        rounds  = config.get("sim"),
    wildcard_constraints:
        rec = "(-4.0|-3.75|-3.5|-3.25|-3.0|-2.75|-2.5|-2.25|-2.0|-1.75|-1.5|-1.25|-1.0|-0.75|-0.5|-0.25|0.0)",
        strict = "(true|false)", 
        simtype = "(flu|kingman)",
        res = "(0.3|0.35|0.4)",
    shell:
        """
        julia --project=../. -J accuracy_shared_branches.so SharedBranches.jl --o "results/results_{wildcards.simtype}_{wildcards.res}_{wildcards.strict}/" --rec {wildcards.rec} --n {params.n_leaves} --sim {params.rounds} --simtype {wildcards.simtype} --res {wildcards.res} --strict {wildcards.strict}     
        """

rule plot_graphs:
    input:
        expand(["results/results_{simtype}_{res}_{strict}/results_{n}_{rec}.txt"], rec=REC, n=NAMES, simtype=SIMTYPE, res=RES, strict=STRICT)
    output:
        "Plots.zip"
    shell:
        """
        mkdir Plots
        julia --project=../. Plot.jl
        zip -rm Plots.zip Plots
        """

rule zip:
    input:
        expand(["results/results_{simtype}_{res}_{strict}/results_{n}_{rec}.txt"], rec=REC, n=NAMES, simtype=SIMTYPE, res=RES, strict=STRICT),
        "Plots.zip"
    output:
        "results.zip"
    shell:
        """
        zip -rm results.zip results
        """
